import org.apache.tools.ant.filters.ReplaceTokens

/*
Enforces compiling with the specified compiler if environment variable JAVA#_HOME is set.
For example, projects that build with Java 7 should have environment variable JAVA7_HOME set.
 */
apply from: scriptFile('env.gradle')
configureJavaCompilers(8)

ext {
    bungeecord = '1.11-SNAPSHOT'
    redis_bungee = '0.3.10'
}

dependencies {
    // Project dependencies.
    compile project(path: ':common', configuration: 'shadow')
    // IntelliJ fails to resolve classes with shadow configuration from project dependencies.
    shadow project(':common')
    shadow project(':rpcapi')
    shadow project(':core')
    // Project specific dependencies.
    provided group: 'net.md-5', name: 'bungeecord-bootstrap', version: bungeecord
    provided group: 'com.imaginarycode.minecraft', name: 'RedisBungee', version: redis_bungee
}

jar {
    archiveName = 'EMPBungee.jar'
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.property("version")
    ]
}

shadowJar {
    archiveName = 'EMPBungee.jar'

    dependencies {
        include(project(':common'))
    }

    relocate 'net.minidev.json', 'com.enjin.shaded.json'
    relocate 'com.google.gson', 'com.enjin.shaded.gson'
    relocate 'com.thetransactioncompany.jsonrpc2', 'com.enjin.shaded.jsonrpc2'
}