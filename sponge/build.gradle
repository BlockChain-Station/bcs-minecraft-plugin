plugins {
    id 'org.spongepowered.plugin' version '0.5.2'
}

sourceCompatibility = 1.8

group = 'com.enjin.sponge'
version = '3.1.5-sponge'

ext {
    sponge = '5.0.0'
}

dependencies {
    // Project dependencies.
    compile project(path: ':common', configuration: 'shadow')
    // IntelliJ fails to resolve classes with shadow configuration from project dependencies.
    shadow project(':common')
    shadow project(':rpcapi')
    shadow project(':core')
    // Project specific dependencies.
    shadow group: 'org.spongepowered', name: "spongeapi", version: sponge
    // File dependencies.
    shadow fileTree(dir: 'lib', include: '*.jar')
}

sponge {
    plugin {
        id = 'enjin-minecraft-plugin'
        meta {
            name = 'Enjin Minecraft Plugin'
            version = "${version}-sponge"
            description = 'Enjin Minecraft Plugin for Sponge!'
        }
    }
}

jar {
    archiveName = 'EMPSponge-slim.jar'
}

shadowJar {
    archiveName = 'EMPSponge.jar'

    dependencies {
        include(project(':common'))
        include(dependency('.*:fanciful'))
    }

    relocate 'net.minidev.json', 'com.enjin.shaded.json'
    relocate 'com.google.gson', 'com.enjin.shaded.gson'
    relocate 'com.thetransactioncompany.jsonrpc2', 'com.enjin.shaded.jsonrpc2'
}