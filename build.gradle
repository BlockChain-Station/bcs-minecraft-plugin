buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply from: 'gradle/util.gradle'

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'com.enjin'
    version = '3.5.0-SNAPSHOT'

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.shadow]
        }
    }

    tasks.build.dependsOn(shadowJar)
    tasks.install.dependsOn(shadowJar)
    artifacts.archives shadowJar

    tasks.withType(AbstractCompile) {
        classpath += configurations.shadow
    }

    ext {
        junit = '4.12'
        lombok = '1.16.8'
        guava = '19.0'
        gson = '2.3.1'
        log4j = '2.8.1'
        jsonrpc2 = '1.16.4'
        json_smart = '1.3.1'
        reflections = '0.9.10'
        javassist = '3.12.1.GA'
        zip4j = '1.3.2'
    }

    repositories {
        mavenLocal()
        mavenCentral()
//        maven { url 'http://maven.enjinpink.com:8081/repository/maven-public/' }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: junit
        shadow group: 'net.minidev', name: 'json-smart', version: json_smart
        shadow group: 'org.projectlombok', name: 'lombok', version: lombok
        shadow group: 'com.google.guava', name: 'guava', version: guava
        shadow group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j
        shadow group: 'com.google.code.gson', name: 'gson', version: gson
        shadow group: 'net.lingala.zip4j', name: 'zip4j', version: zip4j
    }

    shadowJar {
        relocate 'com.google.gson', 'com.enjin.shaded.gson'
        relocate 'net.minidev.json', 'com.enjin.shaded.json'
    }
}