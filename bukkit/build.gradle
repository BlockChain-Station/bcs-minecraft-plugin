import org.apache.tools.ant.filters.*

sourceCompatibility = 1.7

version = '3.0.25'

ext {
    bukkit = '1.11-R0.1-SNAPSHOT'
    fanciful = '0.3.5-SNAPSHOT'
    vault = '1.5.6'
}

dependencies {
    // Project dependencies.
    compile project(path: ':common', configuration: 'shadow')
    // IntelliJ fails to resolve classes with shadow configuration from project dependencies.
    shadow project(':common')
    shadow project(':rpcapi')
    shadow project(':core')
    // Project specific dependencies.
    compile group: 'mkremins', name: 'fanciful', version: fanciful
    compile group: 'org.reflections', name: 'reflections', version: reflections
    compile group: 'javassist', name: 'javassist', version: javassist
    provided group: 'org.spigotmc', name: 'spigot-api', version: bukkit
    provided group: 'net.milkbowl.vault', name: 'Vault', version: vault
    // File dependencies.
    shadow fileTree(dir: 'lib', include: '*.jar')
}

jar {
    archiveName = 'EnjinMinecraftPlugin.jar'
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.property("version")
    ]
}

shadowJar {
    archiveName = 'EnjinMinecraftPlugin.jar'

    dependencies {
        include(project(':common'))
        include(dependency('.*:fanciful'))
        include(dependency('.*:reflections'))
        include(dependency('.*:javassist'))
        include(dependency('.*:zip4j'))
    }

    exclude('mkremins/fanciful/shaded/**')

    relocate 'mkremins.fanciful.shaded', 'com.enjin.shaded'
    relocate 'mkremins.fanciful', 'com.enjin.shaded.fanciful'
    relocate 'org.reflections', 'com.enjin.shaded.reflections'
    relocate 'javassist', 'com.enjin.shaded.javassist'
    relocate 'net.minidev.json', 'com.enjin.shaded.json'
    relocate 'net.amoebaman', 'com.enjin.shaded.amoebaman'
    relocate 'com.google.gson', 'com.enjin.shaded.gson'
    relocate 'com.thetransactioncompany.jsonrpc2', 'com.enjin.shaded.jsonrpc2'
    relocate 'net.lingala.zip4j', 'com.enjin.shaded.zip4j'
}