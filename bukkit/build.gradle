import org.apache.tools.ant.filters.*

/*
Enforces compiling with the specified compiler if environment variable JAVA#_HOME is set.
For example, projects that build with Java 7 should have environment variable JAVA7_HOME set.
 */
apply from: scriptFile('env.gradle')
configureJavaCompilers(8)

ext {
    bukkit = '1.11-R0.1-SNAPSHOT'
    kyoripowered_text = '1.12-1.3.0'
    vault = '1.5.6'
    votifier = '1.9'
    zPermissions = '1.3beta1'
    bPermissions = '2.12.1'
    vanishNoPacket = '3.20'
    tuxTwoLib = '1.11-b7'
    permissionsEx = '1.23.4'
    bukkitPermissions = '2.5'
    mcMMO = '1.5.07-SNAPSHOT'
}

dependencies {
    // Project dependencies.
    compile project(path: ':common', configuration: 'shadow')
    // IntelliJ fails to resolve classes with shadow configuration from project dependencies.
    shadow project(':common')
    shadow project(':rpcapi')
    shadow project(':core')
    // Project specific dependencies.
    compile (group: 'net.kyori', name: 'text', version: kyoripowered_text) {
        exclude group: 'com.google.guava'
    }
    compile group: 'org.reflections', name: 'reflections', version: reflections
    compile group: 'javassist', name: 'javassist', version: javassist
    provided group: 'org.spigotmc', name: 'spigot-api', version: bukkit
    provided group: 'net.milkbowl.vault', name: 'Vault', version: vault
    provided group: 'com.vexsoftware', name: 'votifier', version: votifier
    provided group: 'org.tyrannyofheaven.bukkit', name: 'zPermissions', version: zPermissions
    provided group: 'de.banaco', name: 'bPermissions-Bukkit', version: bPermissions
    provided group: 'org.kitteh', name: 'VanishNoPacket', version: vanishNoPacket
    provided group: 'Tux2', name: 'TuxTwoLib', version: tuxTwoLib
    provided group: 'ru.tehkode', name: 'PermissionsEx', version: permissionsEx
    provided group: 'com.platymuus', name: 'bukkit-permissions', version: bukkitPermissions
    provided group: 'com.gmail.nossr50.mcMMO', name: 'mcMMO', version: mcMMO
    // File dependencies.
    shadow fileTree(dir: 'lib', include: '*.jar')
}

jar {
    archiveName = 'EnjinMinecraftPlugin.jar'
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.property("version")
    ]
}

shadowJar {
    archiveName = 'EnjinMinecraftPlugin.jar'

    dependencies {
        include(project(':common'))
        include(dependency('.*:text'))
        include(dependency('.*:reflections'))
        include(dependency('.*:javassist'))
        include(dependency('.*:zip4j'))
    }

    exclude('mkremins/fanciful/shaded/**')

    relocate 'mkremins.fanciful.shaded', 'com.enjin.shaded'
    relocate 'mkremins.fanciful', 'com.enjin.shaded.fanciful'
    relocate 'org.reflections', 'com.enjin.shaded.reflections'
    relocate 'javassist', 'com.enjin.shaded.javassist'
    relocate 'net.minidev.json', 'com.enjin.shaded.json'
    relocate 'net.amoebaman', 'com.enjin.shaded.amoebaman'
    relocate 'com.google.gson', 'com.enjin.shaded.gson'
    relocate 'com.thetransactioncompany.jsonrpc2', 'com.enjin.shaded.jsonrpc2'
    relocate 'net.lingala.zip4j', 'com.enjin.shaded.zip4j'
}