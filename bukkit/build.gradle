apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

import org.apache.tools.ant.filters.*

sourceCompatibility = 1.7
targetCompatibility = 1.7
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

version = '3.0.16'

jar {
    archiveName = 'EnjinMinecraftPlugin.jar'
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.property("version")
    ]
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name 'com.enjin'
                artifactId 'bukkit'
                packaging 'jar'
                description 'EMP bukkit plugin'
            }
        }
    }
}

shadowJar {
    archiveName = 'EnjinMinecraftPlugin.jar'

    dependencies {
        include(project(':common'))
        include(dependency('.*:fanciful'))
        include(dependency('.*:reflections'))
        include(dependency('.*:javassist'))
    }

    exclude('mkremins/fanciful/shaded/**')

    relocate 'mkremins.fanciful.shaded', 'com.enjin.shaded'
    relocate 'mkremins.fanciful', 'com.enjin.shaded.fanciful'
    relocate 'org.reflections', 'com.enjin.shaded.reflections'
    relocate 'javassist', 'com.enjin.shaded.javassist'
    relocate 'net.minidev.json', 'com.enjin.shaded.json'
    relocate 'net.amoebaman', 'com.enjin.shaded.amoebaman'
    relocate 'com.google.gson', 'com.enjin.shaded.gson'
    relocate 'com.thetransactioncompany.jsonrpc2', 'com.enjin.shaded.jsonrpc2'
}